//@version=5
indicator("Dashboard",overlay= true)
import vkpunique/PineHelper/14 as ph

//General variables
[dailyOpen,dailyHigh,dailyLow,dailyClose] = request.security(syminfo.tickerid, "1D", [open,high,low,close])

//Market Cap Value
series float TSOFQ = request.financial(syminfo.tickerid, "TOTAL_SHARES_OUTSTANDING", "FQ",ignore_invalid_symbol=true)
series float TSOFY =request.financial(syminfo.tickerid, "TOTAL_SHARES_OUTSTANDING", "FY",ignore_invalid_symbol=true)
MarketCap = ph.IsStock()? (na(TSOFQ)?TSOFY:TSOFQ) * close:0

//Free Float Value
series float FSOFY =nz(request.financial(syminfo.tickerid, 'FLOAT_SHARES_OUTSTANDING', 'FY',ignore_invalid_symbol=true))
FreeFloat=ph.IsStock()? FSOFY*ta.vwap:0

////EMA Calculation
float ema10 = request.security(syminfo.tickerid, "1D",ta.ema(close, 10))
float ema21 = request.security(syminfo.tickerid, "1D",ta.ema(close, 21))
float ema50 = request.security(syminfo.tickerid, "1D",ta.ema(close, 50))

bool IsTrendingEMA=(ema10>ema21) and (ema21>ema50)
float ema10_Percentate=ph.Percentage(dailyClose,ema10)
float ema21_Percentate=ph.Percentage(dailyClose,ema21)
float ema50_Percentate=ph.Percentage(dailyClose,ema50)

////Ath Calculatoin
GetAllTimeHigh(dataSeries = high) =>
    highestHTF = request.security(syminfo.tickerid, "D", 
         ph.GetChartHighest(dataSeries)[1], lookahead=barmerge.lookahead_on)
    highestChartTF = ph.GetChartHighest(dataSeries)
    math.max(highestHTF, highestChartTF)

ath_Percentage=ph.Percentage(dailyClose,GetAllTimeHigh()[0])

//// ADR Calculation
adr_Percentage = (request.security(syminfo.tickerid, "1D",ta.sma(high/low, 20)) - 1)

//Fno IsStock
bool isFnoStock= ph.IsFNOStock(syminfo.ticker)

//Industry Trend
var industryData = input.text_area(defval = "", title = "Industry Trend")
GetIndustryTrend(string industry)=>
	var industryMap= ph.GetMapDataInt(industryData)
	industryMap.contains(industry)?industryMap.get(industry):0

// Current Holding 
var holdingData = input.text_area(defval = "", title = "Holdings")
var holdingMap= ph.GetMapDataTrade(holdingData)
float buyPrice=holdingMap.contains(syminfo.ticker)?holdingMap.get(syminfo.ticker).BuyPrice:0
float gain_Percentage=buyPrice>0?ph.Percentage(close,buyPrice) :0

currentDate = timestamp(year(timenow), month(timenow), dayofmonth(timenow), 0, 0)
dateString = holdingMap.contains(syminfo.ticker)?holdingMap.get(syminfo.ticker).OrderDate: str.format_time(currentDate, "yyyyMMdd", syminfo.timezone)

dyear =int( str.tonumber(str.substring(dateString, 0, 4)))
dmonth = int( str.tonumber(str.substring(dateString, 4, 6)))
dday = int( str.tonumber(str.substring(dateString, 6, 8)))

startDate= timestamp(dyear, dmonth,dday, 0, 0)
int NOD= ((currentDate-startDate)/(24 * 60 * 60 * 1000))+1

//GTT Trigger
var triggerData = input.text_area(defval = "", title = "GTT")
var triggerMap= ph.GetMapDataFloat(triggerData)
float trigger=triggerMap.contains(syminfo.ticker)?triggerMap.get(syminfo.ticker):0
trigger_Percentage=trigger>0? ph.Percentage(close,trigger):0
GetTriggerBGColor(x)=>x>=0.02?color.red:x>=-0.02?color.green:color(na)

//// Table Generation
var data= array.new<ph.Result>()
if barstate.islast 
    ph.AddResult(data,"MCAP",ph.ToCrore(MarketCap), ph.GetBGColor(ph.RatioCrore(MarketCap),500,1000,10000,100000,200000))
    ph.AddResult(data, "FF",ph.ToCrore(FreeFloat), ph.GetBGColor(ph.RatioCrore(FreeFloat),300,500,1000,10000,100000))
    ph.AddResult(data)

    ph.AddResult(data,"EMA",IsTrendingEMA?"Pass":"Fail",ph.GetBGColor(IsTrendingEMA))
    ph.AddResult(data,"EMA 10",ph.ToPercentage(ema10_Percentate),ph.GetBGColor(ema10_Percentate,-0.01,0,0.03,0.1,0.15))
    ph.AddResult(data,"EMA 21",ph.ToPercentage(ema21_Percentate),ph.GetBGColor(ema10_Percentate,-0.01,0,0.03,0.1,0.15))
    ph.AddResult(data,"EMA 50",ph.ToPercentage(ema50_Percentate),ph.GetBGColor(ema10_Percentate,-0.01,0,0.03,0.1,0.15))
    ph.AddResult(data,"ATH",ph.ToPercentage(ath_Percentage),ph.GetBGColor(ath_Percentage,-0.2,-0.15,-0.1,-0.01,-0.005))
    ph.AddResult(data,"ADR",ph.ToPercentage(adr_Percentage),ph.GetBGColorUp(adr_Percentage,0,0.025,0.05))
    ph.AddResult(data)

    ph.AddResult(data,"FNO",isFnoStock?"Yes":"No",ph.GetBGColor3(isFnoStock?-1:0))
    ph.AddResult(data,"Industry",syminfo.industry,ph.GetBGColor3(GetIndustryTrend(syminfo.industry)))
    ph.AddResult(data)
    
    ph.AddResult(data,"Gain",buyPrice>0?ph.ToPercentage(gain_Percentage):"-",ph.GetBGColorUp(gain_Percentage,-0.03,0,0.06))
    ph.AddResult(data,"NOD",buyPrice>0?str.tostring(NOD):"-",color(na))
    ph.AddResult(data,"GTT",trigger>0?ph.ToPercentage(trigger_Percentage):"-",ph.GetBGColorDown(trigger_Percentage,0.02,0,-0.02))

    ph.GenerateTable(data,"top_center",ph.TableOrientation.Horizontal)