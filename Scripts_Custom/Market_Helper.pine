//@version=5
indicator("Helper",overlay= true)

//// External data
type Trade
    float BuyPrice
    float Stoploss

var holdingMap= map.new<string,Trade>()
holdingMap.put("IREDA",Trade.new(181.6,231))
holdingMap.put("HBLPOWER",Trade.new(518.6,580))
holdingMap.put("OLECTRA",Trade.new(1903.35,1816))

var triggerMap= map.new<string,float>()
triggerMap.put("GODIGIT",350)
triggerMap.put("IXIGO",176)
triggerMap.put("GMDCLTD",404)
triggerMap.put("MOIL",545)
triggerMap.put("CHAMBLFERT",524)
triggerMap.put("JBMA",2200)
triggerMap.put("TRIVENI",398)
triggerMap.put("KIOCL",483)
triggerMap.put("BALMLAWRIE",295)
triggerMap.put("SUNDARMHLD",276)
triggerMap.put("QUICKHEAL",543)
triggerMap.put("TRACXN",99)
triggerMap.put("ACE",1550)
triggerMap.put("MAPMYINDIA",2458)

// Create variables
var dayHighPrice = 0.0
var dayLowPrice  = 0.0
var prevDayClose = 0.0
var box dailyBox = na

// See if a new calendar day started on the intra-day time frame
newDayStart = dayofmonth != dayofmonth[1] and timeframe.isintraday

// If a new day starts, set the high and low to that bar's data. Else
// during the day track the highest high and lowest low.
if newDayStart
    dayHighPrice := high
    dayLowPrice  := low
    prevDayClose := close[1]
else
    dayHighPrice := math.max(dayHighPrice, high)
    dayLowPrice  := math.min(dayLowPrice, low)

// When a new day start, create a new box for that day.
// Else, during the day, update that day's box.
if newDayStart
    dailyBox := box.new(left=bar_index, top=dayHighPrice,right=bar_index + 1, bottom=dayLowPrice,border_width=1,border_style = line.style_dashed)
    box.set_right(dailyBox[1], bar_index[1])
else
    box.set_top(dailyBox, dayHighPrice)
    box.set_rightbottom(dailyBox, right=bar_index + 1, bottom=dayLowPrice)
    // Box Color Update
    boxColor=close>prevDayClose?color.green:color.red
    box.set_bgcolor(dailyBox, color.new(boxColor, 90))
    box.set_border_color(dailyBox, boxColor)


//Common Inputs
int rowId=0
int colId=0
var textColor=color.white

dayLow=request.security(syminfo.tickerid,"1D",low)
dayHigh=request.security(syminfo.tickerid,"1D",high)
dayOpen=request.security(syminfo.tickerid,"1D",open)
dayClose=request.security(syminfo.tickerid,"1D",close)
preDayClose= request.security(syminfo.tickerid,"1D",close[1])
preDayLow= request.security(syminfo.tickerid,"1D",low[1])

//Trigger Calculation
float trigger=triggerMap.contains(syminfo.ticker)?triggerMap.get(syminfo.ticker):0
trigger_Percentage=trigger>0? (close-trigger)/trigger:0
GetTriggerBGColor(x)=>x>=0.02?color.red:x>=-0.02?(x==0?color(na):color.green):color(na)

//Open Calculation
open_Percentage=(dayOpen-preDayClose)/dayOpen
GetOpenBGColor(x)=>x>=0.01?color.green:x>=-0.01?color(na):color.red

//Gap Calculation
gap_Percentage= (dayLow-preDayClose)/dayLow
GetGapBGColor(x)=>x>=0?color.green:x>=-0.01?color(na):color.red

//Qty Calculation
int positionSize=input.int(300, "Position", minval=200, maxval=1000, step=1)*1000
int qty=math.round(positionSize/close)

//Stoploss
enum sl
    daylow ="DayLow"
    preDayLow  = "Pre DayLow"

slEnum = input.enum(sl.daylow, "Stoploss")
GetSL(x)=>math.round_to_mintick(math.round(x*(1-0.002),2))

float stoploss = switch slEnum
    sl.daylow=>GetSL(dayLow)
    sl.preDayLow=>GetSL(preDayLow)

stoploss_Percentage= (close-stoploss)/stoploss
GetStoplossBGColor(x)=>x>0.04?color.red: x>=0.025?color.orange:x>=0.01?color(na):color.green

//Holding Stock Calculation
float buyPrice=holdingMap.contains(syminfo.ticker)?holdingMap.get(syminfo.ticker).BuyPrice:0
float gain_Percentage=buyPrice>0? (close-buyPrice) /buyPrice:0
GetGainBGColor(x)=>x>=0.15?color.green:x>=0?color(na):x>=-0.03?color.orange:color.red

float sellPrice=holdingMap.contains(syminfo.ticker)?holdingMap.get(syminfo.ticker).Stoploss:0
float sellPrice_Percentage=sellPrice>0? sellPrice/buyPrice-1:0

////Relative Volume Calculation

Round( _val, _decimals) => 
    // Rounds _val to _decimals places.
    _p = math.pow(10,_decimals)
    math.round(math.abs(_val)*_p)/_p*math.sign(_val)

today_vol = request.security(syminfo.tickerid, "D", volume)
yday_vol = request.security(syminfo.tickerid, "D", volume[1])
avg_vol = request.security(syminfo.tickerid, "D", ta.sma(yday_vol, 20))
rvol_Percentate = (today_vol / avg_vol ) 
GetRvolBGColor(x)=>x>=0.8?color.green:x>=0.5?color(na):x>=0.3?color.orange:color.red

//DCR
dcr_Percentage=(dayClose-dayLow)/(dayHigh-dayLow)
GetDcrBGColor(x)=>x>=0.5?color.green:x>=0.4?color.orange:color.red

//Table Generation
method Cells(table tbl,int rowId, int colId,string cellText,color backgroundcolor)=>
    table.cell(tbl,colId,rowId,cellText,text_color = textColor,bgcolor = backgroundcolor)

var dashboard= table.new("top_right",20,3)

dashboard.Cells(rowId,colId,"Gain",color.black)
dashboard.Cells(rowId+1,colId,buyPrice>0? str.tostring(gain_Percentage,"0.00%"):"-",GetGainBGColor(gain_Percentage))
colId:=colId+1

dashboard.Cells(rowId,colId,"Risk",color.black)
dashboard.Cells(rowId+1,colId,sellPrice>0?str.tostring(sellPrice_Percentage,"0.00%"):"-",GetGainBGColor(sellPrice_Percentage))
colId:=colId+1

dashboard.Cells(rowId,colId,"GTT",color.black)
dashboard.Cells(rowId+1,colId,trigger>0?str.tostring(trigger_Percentage,"0.00%"):"-",GetTriggerBGColor(trigger_Percentage))
colId:=colId+1

dashboard.Cells(rowId,colId,"",color(na))
dashboard.Cells(rowId+1,colId,"",color(na))
colId:=colId+1

dashboard.Cells(rowId,colId,"RVol",color.black)
dashboard.Cells(rowId+1,colId, str.tostring(rvol_Percentate,"0.00%"), GetRvolBGColor(rvol_Percentate))
colId:=colId+1
dashboard.Cells(rowId,colId,"Open",color.black)
dashboard.Cells(rowId+1,colId,str.tostring(open_Percentage,"0.00%"),GetOpenBGColor(open_Percentage))
colId:=colId+1
dashboard.Cells(rowId,colId,"Gap",color.black)
dashboard.Cells(rowId+1,colId, str.tostring(gap_Percentage,"0.00%"),GetGapBGColor(gap_Percentage))
colId:=colId+1
dashboard.Cells(rowId,colId,"DCR",color.black)
dashboard.Cells(rowId+1,colId,str.tostring(dcr_Percentage,"0.00%"), GetDcrBGColor(dcr_Percentage))
colId:=colId+1

dashboard.Cells(rowId,colId,"",color(na))
dashboard.Cells(rowId+1,colId,"",color(na))
colId:=colId+1

dashboard.Cells(rowId,colId,"SL%",color.black)
dashboard.Cells(rowId+1,colId,str.tostring(stoploss_Percentage,"0.00%"),GetStoplossBGColor(stoploss_Percentage))
colId:=colId+1

dashboard.Cells(rowId,colId,"SL",color.black)
dashboard.Cells(rowId+1,colId,str.tostring(stoploss),color(na))
colId:=colId+1

dashboard.Cells(rowId,colId,"SL AT",color.black)
dashboard.Cells(rowId+1,colId,str.tostring(slEnum),color(na))
colId:=colId+1

dashboard.Cells(rowId,colId,"QTY",color.black)
dashboard.Cells(rowId+1,colId,str.tostring(qty),color(na))
colId:=colId+1
 