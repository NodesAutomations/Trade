//@version=5 
indicator(title="Main", overlay=true)

//// Plot Moving Averages
show_10d = input(true, "10 Day")
plot(show_10d ? ta.ema(close, 10) : na, color = #ef2a57, title="10 Day")

show_21d = input(true , "21 Day" )
plot(show_21d ? ta.ema(close, 21 ) : na, color = #fc941a, title="21 Day")

show_50d = input(true , "50 Day" )
plot(show_50d ? ta.ema(close, 50 ) : na, color = #4eaf53, title="50 Day")

show_100d = input(false , "100 Day" )
plot(show_100d ? ta.ema(close, 100 ) : na, color = #3768fd, title="100 Day")

show_200d = input(false, "200 Day" )
plot(show_200d ? ta.ema(close, 200 ) : na, color = #fde947, title="200 Day")

//// Plot All time High

// GetChartHighest() returns the highest value of the given argument
// for the entire chart.
GetChartHighest(dataSeries = high) =>
    var chartHighest = dataSeries
    if dataSeries > nz(chartHighest, -1e10)
        chartHighest := dataSeries
    chartHighest

// GetAllTimeHigh() returns the all-time highest value for the given series
GetAllTimeHigh(dataSeries = high) =>
    highestHTF = request.security(syminfo.tickerid, "D", 
         GetChartHighest(dataSeries)[1], lookahead=barmerge.lookahead_on)
    highestChartTF = GetChartHighest(dataSeries)
    math.max(highestHTF, highestChartTF)

// Get and plot the all-time high
plot(series=GetAllTimeHigh(), color=color.white, linewidth=1,title="All-Time High")

//// Bull Snort
show_BullSnort = input(true, "BullSnort" ) and not timeframe.isintraday
if show_BullSnort
    vol = ta.sma(volume, 50)
    if  volume >= (vol*3) and close > close[1] and close > hlc3  == true
        label.new(bar_index, low, yloc= yloc.belowbar, color= color.white, style= label.style_circle , size = size.auto)

//// Inside bar
show_InsideBar=input(true,"InsideBar") and not timeframe.isintraday
data=high[1]>=high and low[1]<=low and show_InsideBar
plotshape(data, style=shape.diamond,location = location.belowbar, color= color.yellow, size = size.tiny, show_last=50)

//// Common Inputs
dailyClose=request.security(syminfo.tickerid,"1D",close)
dailyLow=request.security(syminfo.tickerid,"1D",low)
dailyHigh=request.security(syminfo.tickerid,"1D",high)

////EMA Calculation
ema10 = request.security(syminfo.tickerid, "1D",ta.ema(close, 10))
ema21 = request.security(syminfo.tickerid, "1D",ta.ema(close, 21))
ema50 = request.security(syminfo.tickerid, "1D",ta.ema(close, 50))

ema10_Percentate=(dailyClose-ema10)/ema10
ema21_Percentate=(dailyClose-ema21)/ema21
ema50_Percentate=(dailyClose-ema50)/ema50

GetEmaBGColor(x)=>x>=0.03?color(na):x>=0?color.green:color.red

////Ath Calculatoin

float ath=GetAllTimeHigh()[0]
ath_Percentage=(dailyClose-ath)/ath
GetAthBGColor(x)=>x>=-0.05?color.green:x>=-0.15?color(na):x>=-0.2?color.orange:color.red
 
//// Table Generation
show_dashboard = input(true, "Dashboard" )

var rowId=0
var colId=0
var textColor=color.white

adr_Percentage = 100 * (request.security(syminfo.tickerid, "1D",ta.sma(high/low, 20)) - 1)
GetAdrBGColor(x)=>x>=5?color.green:x>=3.5?color(na):(x>=2.5?color.orange:color.red)

method Cells(table tbl,int rowId, int colId,string cellText,color backgroundcolor)=>
    table.cell(tbl,colId,rowId,cellText,text_color = textColor,bgcolor = backgroundcolor)

if barstate.islast and show_dashboard
    var dashboard= table.new("top_center",10,2)

    dashboard.Cells(rowId,colId,"EMA 10",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(ema10_Percentate,"0.00%"), GetEmaBGColor(ema10_Percentate))
    colId:=colId+1

    dashboard.Cells(rowId,colId,"EMA 21",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(ema21_Percentate,"0.00%"), GetEmaBGColor(ema21_Percentate))
    colId:=colId+1

    dashboard.Cells(rowId,colId,"EMA 50",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(ema50_Percentate,"0.00%"), GetEmaBGColor(ema50_Percentate))
    colId:=colId+1
 
    dashboard.Cells(rowId,colId,"ATH",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(ath_Percentage,"0.00%"), GetAthBGColor(ath_Percentage))
    colId:=colId+1

    dashboard.Cells(rowId,colId,"ADR",color.black)  
    dashboard.Cells(rowId+1,colId, str.tostring(adr_Percentage,"0.00")+"%", GetAdrBGColor(adr_Percentage))
    colId:=colId+1