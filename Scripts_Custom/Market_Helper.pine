//@version=5
indicator("Helper",overlay= true)

// Create variables
var dayHighPrice = 0.0
var dayLowPrice  = 0.0
var prevDayClose = 0.0
var box dailyBox = na

// See if a new calendar day started on the intra-day time frame
newDayStart = dayofmonth != dayofmonth[1] and timeframe.isintraday

// If a new day starts, set the high and low to that bar's data. Else
// during the day track the highest high and lowest low.
if newDayStart
    dayHighPrice := high
    dayLowPrice  := low
    prevDayClose := close[1]
else
    dayHighPrice := math.max(dayHighPrice, high)
    dayLowPrice  := math.min(dayLowPrice, low)

// When a new day start, create a new box for that day.
// Else, during the day, update that day's box.
if newDayStart
    dailyBox := box.new(left=bar_index, top=dayHighPrice,right=bar_index + 1, bottom=dayLowPrice,border_width=1,border_style = line.style_dashed)
    box.set_right(dailyBox[1], bar_index[1])
else
    box.set_top(dailyBox, dayHighPrice)
    box.set_rightbottom(dailyBox, right=bar_index + 1, bottom=dayLowPrice)
    // Box Color Update
    boxColor=close>prevDayClose?color.green:color.red
    box.set_bgcolor(dailyBox, color.new(boxColor, 90))
    box.set_border_color(dailyBox, boxColor)

int rowId=0
int colId=0
var textColor=color.white

//Chart info
dayLow=request.security(syminfo.tickerid,"1D",low)
dayOpen=request.security(syminfo.tickerid,"1D",open)
preDayClose= request.security(syminfo.tickerid,"1D",close[1])
preDayLow= request.security(syminfo.tickerid,"1D",low[1])

method Cells(table tbl,int rowId, int colId,string cellText,color backgroundcolor)=>
    table.cell(tbl,colId,rowId,cellText,text_color = textColor,bgcolor = backgroundcolor)

//Open Calculation
open_Percentage=(dayOpen-preDayClose)/dayOpen
GetOpenBGColor(x)=>x>=0.01?color.green:x>=-0.005?color.orange:color.red

//Gap Calculation
gap_Percentage= (dayLow-preDayClose)/dayLow
GetGapBGColor(x)=>x>=0?color.green:x>=-0.01?color.orange:color.red

//Qty Calculation
int positionSize=input.int(300, "Position", minval=200, maxval=1000, step=1)*1000
int qty=math.round(positionSize/close)

//Stoploss
enum sl
    daylow ="Day Low"
    preDayLow  = "Pre Day Low"

slEnum = input.enum(sl.daylow, "Stoploss")
GetSL(x)=>math.round(x*(1-0.002),2)

float stoploss = switch slEnum
    sl.daylow=>GetSL(dayLow)
    sl.preDayLow=>GetSL(preDayLow)

stoploss_Percentage= (close-stoploss)/stoploss
GetStoplossBGColor(x)=>x<=0.02?color.green:x<=0.04?color.orange:color.red

var dashboard= table.new("top_right",2,10)
dashboard.Cells(rowId,colId,"Helper",color.blue)
//Merge First Row
table.merge_cells(dashboard,colId,rowId,colId+1,rowId)
rowId:=rowId+2

dashboard.Cells(rowId,colId,"Open",color.black)
dashboard.Cells(rowId,colId+1, str.tostring(open_Percentage,"0.00%"),GetOpenBGColor(open_Percentage))
rowId:=rowId+1

dashboard.Cells(rowId,colId,"Gap",color.black)
dashboard.Cells(rowId,colId+1, str.tostring(gap_Percentage,"0.00%"),GetGapBGColor(gap_Percentage))
rowId:=rowId+1

dashboard.Cells(rowId,colId,"SL%",color.black)
dashboard.Cells(rowId,colId+1,str.tostring(stoploss_Percentage,"0.00%"),GetStoplossBGColor(stoploss_Percentage))
rowId:=rowId+1

dashboard.Cells(rowId,colId,"SL",color.black)
dashboard.Cells(rowId,colId+1,str.tostring(stoploss),GetStoplossBGColor(stoploss_Percentage))
rowId:=rowId+1

dashboard.Cells(rowId,colId,"QTY",color.black)
dashboard.Cells(rowId,colId+1, str.tostring(qty),color.green)
rowId:=rowId+1

