//@version=5 
indicator(title="Main", overlay=true)

//// Plot Moving Averages
show_10d = input(true, "10 Day")
plot(show_10d ? ta.ema(close, 10) : na, color = #ef2a57, title="10 Day")

show_21d = input(true , "21 Day" )
plot(show_21d ? ta.ema(close, 21 ) : na, color = #fc941a, title="21 Day")

show_50d = input(true , "50 Day" )
plot(show_50d ? ta.ema(close, 50 ) : na, color = #4eaf53, title="50 Day")

show_100d = input(false , "100 Day" )
plot(show_100d ? ta.ema(close, 100 ) : na, color = #3768fd, title="100 Day")

show_200d = input(false, "200 Day" )
plot(show_200d ? ta.ema(close, 200 ) : na, color = #fde947, title="200 Day")

//// Plot All time High

// GetChartHighest() returns the highest value of the given argument
// for the entire chart.
GetChartHighest(dataSeries = high) =>
    var chartHighest = dataSeries
    if dataSeries > nz(chartHighest, -1e10)
        chartHighest := dataSeries
    chartHighest

// GetAllTimeHigh() returns the all-time highest value for the given series
GetAllTimeHigh(dataSeries = high) =>
    highestHTF = request.security(syminfo.tickerid, "D", 
         GetChartHighest(dataSeries)[1], lookahead=barmerge.lookahead_on)
    highestChartTF = GetChartHighest(dataSeries)
    math.max(highestHTF, highestChartTF)

// Get and plot the all-time high
plot(series=GetAllTimeHigh(), color=color.white, linewidth=1,title="All-Time High")

//// Bull Snort
show_BullSnort = input(false, "BullSnort" )
if show_BullSnort
    vol = ta.sma(volume, 50)
    if  volume >= (vol*3) and close > close[1] and close > hlc3  == true
        label.new(bar_index, low, yloc= yloc.belowbar, color= color.yellow, style= label.style_triangleup , size = size.tiny)

////EMA Calculation
ema10 = ta.ema(close, 10)
ema21 = ta.ema(close, 21)
ema50 = ta.ema(close, 50)

ema10_Percentate=(close-ema10)/ema10
ema21_Percentate=(close-ema21)/ema21
ema50_Percentate=(close-ema50)/ema50

GetEmaBGColor(x)=>x>=0?color.green:x>=-0.02?color.orange:color.red

////Ath Calculatoin

float ath=GetAllTimeHigh()[0]
ath_Percentage=(close-ath)/ath
GetAthBGColor(x)=>x>=-0.15?color.green:x>=-0.2?color.orange:color.red

////Relative Volume Calculation

Round( _val, _decimals) => 
    // Rounds _val to _decimals places.
    _p = math.pow(10,_decimals)
    math.round(math.abs(_val)*_p)/_p*math.sign(_val)

today_vol = request.security(syminfo.tickerid, "D", volume)
yday_vol = request.security(syminfo.tickerid, "D", volume[1])
avg_vol = request.security(syminfo.tickerid, "D", ta.sma(yday_vol, 20))
rvol_Percentate = (today_vol / avg_vol ) 

GetRvolBGColor(x)=>x>=0.5?color.green:x>=0.2?color.orange:color.red

//// Table Generation
show_dashboard = input(true, "Dashboard" )

var rowId=0
var colId=0
var textColor=color.white
dcr_Percentage=(close-low)/(high-low)
GetDcrBGColor(x)=>x>=0.5?color.green:color.red
adr_Percentage = 100 * (ta.sma(high/low, 20) - 1)
GetAdrBGColor(x)=>x>=5?color.green:x>=4?color.orange:color.red

method Cells(table tbl,int rowId, int colId,string cellText,color backgroundcolor)=>
    table.cell(tbl,colId,rowId,cellText,text_color = textColor,bgcolor = backgroundcolor)

if barstate.islast and show_dashboard
    var dashboard= table.new("top_center",10,2)

    dashboard.Cells(rowId,colId,"EMA 10",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(ema10_Percentate,"0.00%"), GetEmaBGColor(ema10_Percentate))
    colId:=colId+1

    dashboard.Cells(rowId,colId,"EMA 21",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(ema21_Percentate,"0.00%"), GetEmaBGColor(ema21_Percentate))
    colId:=colId+1

    dashboard.Cells(rowId,colId,"EMA 50",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(ema50_Percentate,"0.00%"), GetEmaBGColor(ema50_Percentate))
    colId:=colId+1
 
    dashboard.Cells(rowId,colId,"ATH",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(ath_Percentage,"0.00%"), GetAthBGColor(ath_Percentage))
    colId:=colId+1

    dashboard.Cells(rowId,colId,"RVol",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(rvol_Percentate,"0.00%"), GetRvolBGColor(rvol_Percentate))
    colId:=colId+1

    dashboard.Cells(rowId,colId,"DCR",color.black)
    dashboard.Cells(rowId+1,colId, str.tostring(dcr_Percentage,"0.00%"), GetDcrBGColor(dcr_Percentage))
    colId:=colId+1

    dashboard.Cells(rowId,colId,"ADR",color.black)  
    dashboard.Cells(rowId+1,colId, str.tostring(adr_Percentage,"0.00")+"%", GetAdrBGColor(adr_Percentage))
    colId:=colId+1
